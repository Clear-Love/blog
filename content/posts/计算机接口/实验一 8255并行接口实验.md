---
title: "8255并行接口控制led灯"
date: 2023-05-26T06:02:28+08:00
draft: false
tags: ["计算机接口", "嵌入式"]
categories: ["计算机接口"]
series: ["计算机接口实验"]
featuredImage: https://pan.lmio.xyz/pic/20221127212857.png
toc:
  enable: true
---



## 一、实验目的

1. 学会利用 `pc` 总线组织一个并行接口的基本方法； 
2. 了解并行接口电路 `8255A` 的基本性能，掌握其引脚连接方法和初始化编程方法；
3. 初步学会对并行端口进行一般输出控制的方法。


## 二、实验内容

- 用 `8255A` 扩展一组（3 个）并行口，利用这些并行口连接 `24` 个发光二极管，使之组成
一个连环，然后通过编程控制这些发光二极管，使其按照一定规律交替闪亮。


## 三、实验步骤



- 实验接线图

![](https://pan.lmio.xyz/pic/20221127210832.png)

- 实验程序（含注释或流程图）

	**流程图：**
		![](https://pan.lmio.xyz/pic/20221127212857.png)

	**代码**
```armasm
data segment
        portA equ 400H                                                                                ; PA口地址
        s     db  "twenty four LEDs flicker circularly. Press any key to exit ...",0dh,0ah,"$"        ;程序功能提示
data ends
code segment
                assume cs:code,ds:data
        go:     mov    ax,data
                mov    ds,ax
                mov    dx,offset s            ;显示提示
                mov    ah,9
                int    21h
                mov    dx,porta+3
                mov    al,80h
                Out    dx,al                  ; 三端口均初始化为方式0输出
        L0:     mov    si,24
                mov    bx,7                   ;  低16位初始值设为1（一个灯亮）
                mov    cl,0                   ;  高8位初始值设为0
        L1:     mov    ah,0bH
                Int    21h                    ; 有无按键
                Cmp    al,0ffH
                Jz     exit
                mov    dx,porta               ; 输出24位数
                mov    al,bl
                Out    dx,al
                Inc    dx
                mov    al,bh
                Out    dx,al
                Inc    dx
                mov    al,cl
                Out    dx,al
                Call   delay05                ; 延时
                SHL    bx,1                   ;  低16位左移一位
                Rcl    cl,1                   ;  高8位左移一位
                dec    si
                jnz    L1
                Jmp    L0                     ; 重新开始
        Exit:   mov    ah,4ch
                Int    21h
delay05 proc near                             ; 循环延时子程序
                push   cx
                push   bx
                mov    cx,0020H
        d1:     mov    bx,1200h
        d2:     dec    bx
                jnz    d2
                loop   d1
                pop    bx
                pop    cx
                ret
delay05 endp
Code ends
        End go


```

- 实验结果

![](https://pan.lmio.xyz/pic/%E5%AE%9E%E9%AA%8C%E4%B8%80.gif)



## 四、实验小结

 利用 `8255` 的三个端口输出 `24` 个数字信号，经过 `74LS07` 反相驱动器接至各 LED 的阴极端，LED 阳极通过上拉电阻接至 `+5V` 电源，当 `8255` 输出高电平时对应的 LED 可发亮，输出低电平则灭，这样便可通过编程，每间隔一段时间，通过 `8255` 改变输出数据，使 LED 按照一定的规律闪亮变化。