---
title: "8254定时器模拟电子琴弹奏"
date: 2023-05-26T06:02:28+08:00
draft: false
tags: ["计算机接口", "嵌入式"]
categories: ["计算机接口"]
series: ["计算机接口实验"]
series_weight: 1
featuredImage: https://pan.lmio.xyz/pic/20221205111048.png
toc:
  enable: true
---

  
**一、实验目的**

1．了解音符的实现和扬声器发声原理。

2．掌握用8254定时/计数器使扬声器发声的编程方法。

**二、实验内容**

1．根据实验提供的音乐频率表和时间表，编写程序控制8254，使其输出连接到扬声器上能发出相应的乐曲。

2．利用8254电路和小键盘实验台编程实现可模拟电子琴弹奏的功能。

**三、实验步骤**

   实验接线图、实验程序（含注释或流程图）、实验结果（含现场拍照图及对应的实验结果描述）、实验结论。
   
  - 实验接线图
  ![](https://pan.lmio.xyz/pic/174dbf38ea259d848a0e05be6283d7a.png)

- 实验流程图
![](https://pan.lmio.xyz/pic/20221205111048.png)

- 实验程序
```armasm
;  演奏“友谊地久天长”——使用8254虚拟电路
DATA     SEGMENT 
FREQ_LIST  DW  371,495,495,495,624,556,495,556,624  ;频率表
           DW  495,495,624,742,833,833,833,742,624
           DW  624,495,556,495,556,624,495,416,416,371
   	       DW  495,833,742,624,624,495,556,495,556,833
   	       DW  742,624,624,742,833,990,742,624,624,495
           DW  556,495,556,624,495,416,416,371,495,0
TIME_LIST  DB  4,6,2,4,4,6,2,4,4                    ;时间表
     	    DB  6,2,4,4,12,1,3,6,2
     	    DB  4,4,6,2,4,4,6,2,4,4
     	    DB 12,4,6,2,4,4,6,2,4,4
           DB  6,2,4,4,12,4,6,2,4,4
     	    DB  6,2,4,4,6,2,4,4,12
TIM_CTL    EQU 453H       ;8254 状态/命令口地址
TIMER1     EQU 451H       ;8254 定时计数器1地址
MODE00     db 70h
MODE03     db 76h
sound_FREQ dw  523,587,659,698,784,880,988,1046,1175,1318,1397, 1568,1760,1967
s          db "Playing a tune. Please wait a moment...",0dh,0ah,"$" 
DATA     ENDS
code     segment
           assume  cs:code,ds:data
go:        mov ax,data
           mov ds,ax 
           mov dx,offset s     ; 显示提示
           mov ah,9
           int 21h
           mov dx,TIM_CTL      ; 定时器1工作在方式3
           mov al,MODE03
           out dx,al  
           LEA SI,OFFSET FREQ_LIST
           LEA DI,OFFSET TIME_LIST
m1:        cmp WORD PTR [si],0
           jz exit
           mov ah,0bh
           int 21h   ; 检测有无按键，返回AL=0FFh(有按键)或0(无按键)
           cmp al,0FFh
           jz Exit
           call sound    
           call DALLY       
           inc si
           inc si
           inc di
           loop m1
exit:  
           mov dx,TIM_CTL      ; 定时器1工作在方式0(关闭)
           mov al,MODE00
           out dx,al    
           mov ah,4ch       ; 结束
           int 21h
     
DALLY    PROC         ;延时子程序。若电脑速度慢可减少CX或AX的值
           push ax
           push bx
           push cx
           mov bl,[di]
D0:        MOV CX,12H
D1:        MOV AX,02000H
D2:        DEC AX
           JNZ D2 
           LOOP D1
           dec bl
           jnz d0
           pop cx
           pop bx
           pop ax
	       RET
DALLY    ENDP

sound    PROC         ;发音
         push ax
         push bx
         push cx
         push dx
         MOV DX,0fH                  ;输入时钟为1MHz，1M = 0F4240H  
         MOV AX,4240h               
         DIV  WORD PTR [SI] ;取出频率值计算计数初值，0F4240H / 输出频率  
         MOV cx,AX
         MOV dx,TIMER1       ; 定时器1计数初值 
         MOV al,cl
         out dx,al
         MOV al,ch
         out dx,al 
         pop dx
         pop cx
         pop bx
         pop ax
         ret 
sound   ENDP
code ends
     END  go

```

```armasm
; 模拟电子琴程序——需同时使用8254和小键盘虚拟电路
data segment
PortA equ 408H      ; 小键盘8255的PA口地址 
TIM_CTL EQU 453H    ;8254 状态/命令口地址
TIMER1  EQU 451H
MODE00  db 70h
MODE03  db 76h
sound_FREQ  DW 494,523,587,659,698,784,880,988 ;0~7键
            DW 1046,1175,1318,1397,1568,1760,1975,0 ;8~F键
s           db "Use 8254 and keypad circuits.Press any key to exit...",0dh,0ah,"$"    ;程序功能提示
data ends
code segment
     assume  cs:code,ds:data
go:  mov ax,data
     mov ds,ax 
     mov dx,offset s     ; 显示提示
     mov ah,9
     int 21h
      mov dx, PortA+3
      mov al,82H        ;  工作方式：PA输出 PB输入
      out dx,al          
l0:
      mov si,0
      mov al,0feh       ; 初始扫描码
      mov bx,04h
l1:   mov dx,PortA
      out dx,al
      push ax
      mov dx,portA+1
      in al,dx
      mov cx,04h
      mov di,0     
l2:   shr al,1
      jc l3
      call sound        ;得到的键值为si*4+di,对应0~15
l3:   inc di
      loop l2
      pop ax
      rol al,1
      inc si
      dec bx
      jnz l1
     
      mov ah,0bh
      int 21h   ; 检测有无按键，返回AL=0FFh(有按键)或0(无按键)
      cmp al,0FFh
      jnz l0
     mov ah,4ch       ; 结束
     int 21h
sound    PROC         ;发音
     push ax
     push bx
     push cx
     push dx
     push si
     push di
     add si,si   ;si*4+di
     add si,si
     add si,di
     add si,si   ;si*2 每个单元为2字节
     lea bx,sound_FREQ
     cmp WORD PTR[SI+BX],0
     jnz s1
     mov dx,TIM_CTL      ; 定时器1工作在方式0（用于关声音）
     mov al,MODE00
     out dx,al    
     jmp sexit     
s1:
     mov dx,TIM_CTL      ; 定时器1工作在方式3
     mov al,MODE03
     out dx,al  
    MOV DX,0fH            ; 输入时钟为1MHz，1M = 0F4240H  
    MOV AX,4240h               
    DIV  WORD PTR [SI+BX] ; 取出频率值计算计数初值，0F4240H / 音符频率  
    MOV cx,AX             ; 定时器1计数初值
     MOV dx,TIMER1        
     MOV al,cl
     out dx,al
     MOV al,ch
     out dx,al 
sexit:     
    pop di
    pop si 
    pop dx
    pop cx
    pop bx
    pop ax
    ret 
sound    ENDP
     
DALLY    PROC         ;延时子程序
     push cx
     push ax
     MOV CX,20H
D1:  MOV AX,0ffffH
D2:  DEC AX
     JNZ D2 
     LOOP D1
     pop ax
     pop cx
		RET
DALLY    ENDP
code ends
     END  go


```
- 实验结果

![](https://pan.lmio.xyz/pic/d51f6e1a619ec5e2942d23962f3723c.png)

![](https://pan.lmio.xyz/pic/d0098f0e80d2441d5eec050a838f27e.png)

**四、** **实验小结**

掌握的音乐产生的原理，一个音符对应一个频率，将对应一个音符频率的方波输出到扬声器上，就可以发出这个音符的声音。将一段乐曲的音符对应频率的方波依次送到扬声器，就可以发出这段乐曲的声音。